buildscript {
    repositories {
        jcenter()
        google()
//        mavenLocal()
        maven { url "file://${projectDir.absolutePath}/../build/localMaven" }
        maven { url "https://dl.bintray.com/yshrsmz/kgql" }
    }
    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.71'
        classpath 'org.jetbrains.kotlin:kotlin-serialization:1.3.71'
        classpath 'com.codingfeline.kgql:gradle-plugin:+'
    }
}

apply plugin: 'kotlin-multiplatform'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.codingfeline.kgql'

repositories {
    jcenter()
    google()
//    mavenLocal()
    maven { url "file://${projectDir.absolutePath}/../build/localMaven" }
    maven { url "https://dl.bintray.com/yshrsmz/kgql" }
}

kotlin {
    jvm()
    js()
    ios {
        binaries {
            framework()
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:0.20.0"
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.20.0"
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:0.20.0"
            }
        }
        iosMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-native:0.20.0"
            }
        }
    }
}

kgql {
    packageName = "com.codingfeline.kgql.sample"
    sourceSet = files("src/main/kgql")
}

// Workaround for https://youtrack.jetbrains.com/issue/KT-36721.
// Put this snippet into your root buildscript.
allprojects {
    pluginManager.withPlugin("kotlin-multiplatform") {
        def uniqueName = "${project.group}.${project.name}".toString()

        project.kotlin.targets.withType(org.jetbrains.kotlin.gradle.plugin.mpp.KotlinNativeTarget.class) {
            compilations["main"].kotlinOptions.freeCompilerArgs += ["-module-name", uniqueName]
        }
    }
}