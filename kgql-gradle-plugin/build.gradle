plugins {
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.pluginPublish)
    id("java-gradle-plugin")
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8


pluginBundle {
    website = POM_URL
    vcsUrl = 'https://github.com/yshrsmz/kgql.git'
    description = POM_DESCRIPTION
    tags = ['GraphQL', 'Kotlin', 'Kotlin Multiplatform']

    plugins {
        kgql {
            displayName = POM_NAME
        }
    }
}

gradlePlugin {
    plugins {
        kgql {
            id = "com.codingfeline.kgql"
            implementationClass = "com.codingfeline.kgql.gradle.KgqlPlugin"
        }
    }
}

configurations {
    fixtureClasspath
}

// Append any extra dependencies to the test fixtures via a custom configuration classpath. This
// allows us to apply additional plugins in a fixture while still leveraging dependency resolution
// and de-duplication semantics.
tasks.getByName('pluginUnderTestMetadata')
        .getPluginClasspath()
        .from(configurations.fixtureClasspath)


dependencies {
    implementation project(':kgql-compiler')

    implementation libs.kotlin.nativeUtils
    implementation libs.kotlin.plugin
    implementation libs.kotlin.plugin.serialization
    implementation libs.android.plugin

    compileOnly gradleApi()

    testImplementation libs.junit
    testImplementation libs.truth

    fixtureClasspath libs.kotlin.plugin
    fixtureClasspath libs.kotlin.plugin.serialization
    fixtureClasspath libs.android.plugin
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    def os = System.getenv("TRAVIS_OS_NAME")
    dependsOn(
            ":kgql-core:publishAllPublicationsToTestMavenRepository",
            ":kgql-compiler:publishAllPublicationsToTestMavenRepository",
    )
}

// work around for https://youtrack.jetbrains.com/issue/KT-27059
configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module("${project.property("GROUP")}:core-jvm:${project.property("VERSION_NAME")}") with project(':kgql-core')
    }
}

apply from: "$rootDir/gradle/maven-publish.gradle"
