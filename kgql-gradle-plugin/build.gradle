apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'java-gradle-plugin'
apply plugin: "com.gradle.plugin-publish"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8


pluginBundle {
    website = POM_URL
    vcsUrl = 'https://github.com/yshrsmz/kgql.git'
    description = POM_DESCRIPTION
    tags = ['GraphQL', 'Kotlin', 'Kotlin Multiplatform']

    plugins {
        kgql {
            displayName = POM_NAME
        }
    }
}

gradlePlugin {
    plugins {
        kgql {
            id = "com.codingfeline.kgql"
            implementationClass = "com.codingfeline.kgql.gradle.KgqlPlugin"
        }
    }
}

configurations {
    fixtureClasspath
}

// Append any extra dependencies to the test fixtures via a custom configuration classpath. This
// allows us to apply additional plugins in a fixture while still leveraging dependency resolution
// and de-duplication semantics.
tasks.getByName('pluginUnderTestMetadata')
        .getPluginClasspath()
        .from(configurations.fixtureClasspath)


dependencies {
    implementation project(':kgql-compiler')

    compileOnly gradleApi()
    implementation deps.kotlin.stdlib.jdk8
    implementation deps.kotlin.reflect
    implementation deps.kotlin.nativeUtils
    implementation deps.plugins.kotlin
    implementation deps.plugins.serialization
    implementation deps.plugins.android

    testImplementation deps.junit
    testImplementation deps.truth

    fixtureClasspath deps.plugins.kotlin
    fixtureClasspath deps.plugins.serialization
    fixtureClasspath deps.plugins.android
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    def os = System.getenv("TRAVIS_OS_NAME")
    dependsOn(
            ":kgql-core:installArchives",
    )
}

// work around for https://youtrack.jetbrains.com/issue/KT-27059
configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module("${project.property("GROUP")}:core-jvm:${project.property("VERSION_NAME")}") with project(':kgql-core')
    }
}

apply from: "$rootDir/gradle/gradle-mvn-push.gradle"
