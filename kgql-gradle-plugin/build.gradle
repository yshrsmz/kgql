plugins {
    alias(libs.plugins.kotlin.jvm)
    id("java-gradle-plugin")
    alias(libs.plugins.mavenPublish)
    alias(libs.plugins.dokka)
    alias(libs.plugins.pluginPublish)
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

pluginBundle {
    website = POM_URL
    vcsUrl = 'https://github.com/yshrsmz/kgql.git'
    tags = ['GraphQL', 'Kotlin', 'Kotlin Multiplatform']
}

gradlePlugin {
    plugins {
        kgql {
            id = "com.codingfeline.kgql"
            displayName = POM_NAME
            description = POM_DESCRIPTION
            implementationClass = "com.codingfeline.kgql.gradle.KgqlPlugin"
        }
    }
}

dependencies {
    implementation project(':kgql-compiler')

    implementation libs.kotlin.nativeUtils
    implementation libs.kotlin.plugin
    implementation libs.kotlin.plugin.serialization
    implementation libs.android.plugin

    compileOnly gradleApi()

    testImplementation libs.junit
    testImplementation libs.truth
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    def os = System.getenv("TRAVIS_OS_NAME")
    dependsOn(
            ":kgql-core:publishAllPublicationsToTestMavenRepository",
            ":kgql-compiler:publishAllPublicationsToTestMavenRepository",
    )
}

// work around for https://youtrack.jetbrains.com/issue/KT-27059
configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module("${project.property("GROUP")}:core-jvm:${project.property("VERSION_NAME")}") with project(':kgql-core')
    }
}

apply from: "$rootDir/gradle/maven-publish.gradle"
