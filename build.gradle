apply plugin: 'com.github.ben-manes.versions'

buildscript {
    apply from: "$rootDir/gradle/dependencies.gradle"

    repositories {
        mavenCentral()
        google()
        jcenter()
        gradlePluginPortal()

        dependencies {
            classpath deps.plugins.kotlin
            classpath deps.plugins.serialization
            classpath deps.plugins.dokka
            classpath deps.plugins.android
            classpath deps.plugins.versions
            classpath deps.plugins.pluginPublish
        }
    }
}


allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    group = GROUP
    version = VERSION_NAME
}

wrapper {
    gradleVersion = '5.6.4'
    distributionType = Wrapper.DistributionType.ALL
}

// TODO work around https://youtrack.jetbrains.com/issue/KT-27059
@SuppressWarnings(["UnnecessaryQualifiedReference", "unused", "GrMethodMayBeStatic"])
def addDependency(Upload task, String groupId, String artifactId, String version, String type) {
    task.repositories.withType(org.gradle.api.artifacts.maven.MavenDeployer) { mavenDeployer ->
        task.doFirst {
            mavenDeployer.pom.whenConfigured {
                // For whatever reason we can't 'new' these ourselves. Clone an existing instance instead.
                def dependency = it.dependencies.get(0).clone()
                dependency.groupId = groupId
                dependency.artifactId = artifactId
                dependency.version = version
                dependency.type = type
                it.dependencies.add(dependency)
            }
        }
    }
}
