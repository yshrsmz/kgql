import java.nio.file.Files
import java.nio.file.Paths

apply plugin: 'com.github.ben-manes.versions'

buildscript {
    apply from: "$rootDir/gradle/dependencies.gradle"

    repositories {
        mavenCentral()
        google()
        gradlePluginPortal()

        dependencies {
            classpath deps.plugins.kotlin
            classpath deps.plugins.serialization
            classpath deps.plugins.dokka
            classpath deps.plugins.android
            classpath deps.plugins.versions
            classpath deps.plugins.pluginPublish
            classpath deps.plugins.mavenPublish
        }
    }
}


allprojects {
    repositories {
        google()
        mavenCentral()
    }

    group = GROUP
    version = VERSION_NAME
}

wrapper {
    gradleVersion = '7.0.2'
    distributionType = Wrapper.DistributionType.ALL
}

// Load `secret.properties` file, if it exists. You can put your bintrayUser and bintrayApiKey values there, that file is ignored by git
if (Files.exists(Paths.get("$project.rootDir/secret.properties"))) {
    def localProperties = new Properties()
    localProperties.load(new FileInputStream("$project.rootDir/secret.properties"))
    localProperties.each { prop -> project.ext.set(prop.key, prop.value) }
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}
tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}